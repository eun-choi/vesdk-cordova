def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

// Apply the VESDKPlugin.
// [!] This assumes you made the correct changes to your projects build.gradle file!
//     See https://docs.photoeditorsdk.com/guides/android/v7/introduction/getting_started for details.
apply plugin: 'ly.img.android.sdk'
apply plugin: 'kotlin-android'

// Configure the VESDKPlugin
IMGLY.predefine {
    vesdk {
        enabled true
    }

    useInheritanceDependencies true

    modules {
        include 'ui:core'
        include 'backend:config-loader'
    }
}

def MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION = "10.1.1"

task checkVESDKVersion {
    if (imglyConfig.convertToVersionNumber(imglyConfig.getVersion()) < imglyConfig.convertToVersionNumber(MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION)) {
        String WRONG_VERSION = "" +
            "┌───────────────────────────────────────────────────────────────────────┐\n"+
            "│ VideoEditor SDK update required!                                      │\n"+
            "│ Please update the plugin version in your 'android/build.gradle' file. │\n"+
            "│ Minimum required dependency: 'ly.img.android.sdk:plugin:${MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION}'       │\n"+
            "│ Your are currently using:    'ly.img.android.sdk:plugin:${imglyConfig.getVersion()}'       │\n"+
            "└───────────────────────────────────────────────────────────────────────┘\n"

        throw new GradleScriptException(WRONG_VERSION, new IllegalArgumentException(WRONG_VERSION))
    }
}

preBuild.dependsOn checkVESDKVersion

android {
    kotlinOptions {
        jvmTarget = "17"
    }

    compileSdkVersion safeExtGet('compileSdkVersion', 33)
    buildToolsVersion safeExtGet('buildToolsVersion', '33.0.3')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 24)
        targetSdkVersion safeExtGet('targetSdkVersion', 33)
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        all {
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.22'
    compileOnly "ly.img.android.sdk:serializer:$imglyConfig.version"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
}